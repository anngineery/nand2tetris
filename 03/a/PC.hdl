// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // need to pay attention to the order or if statement to find out the precedence of control signals
    // precedence: reset > load > inc > no-op
    Register(in=resetMuxOut, load=true, out=out, out=feedbackOut); // introduce time delay
    Mux16(a=loadMuxOut, b=false, sel=reset, out=resetMuxOut);
    Mux16(a=incMuxOut, b=in, sel=load, out=loadMuxOut);
    Mux16(a=feedbackOut, b=incOut, sel=inc, out=incMuxOut);
    Inc16(in=feedbackOut, out=incOut);
}
